apply from: 'gradle/scripts/helpers.gradle'

repositories {
    // Other repositories described by default:
    // CleanroomMC: https://maven.cleanroommc.com
    exclusiveContent {
        forRepository {
            maven {
                name 'CurseMaven'
                url 'https://cursemaven.com'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name 'Modrinth'
                url 'https://api.modrinth.com/maven'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    mavenLocal() // Must be last for caching to work
}

dependencies {
    // Include StripLatestForgeRequirements by default for the dev env, saves everyone a hassle
    runtimeOnly 'com.cleanroommc:strip-latest-forge-requirements:1.0'

    // Example - Dependency descriptor:
    // 'com.google.code.gson:gson:2.8.6' << group: com.google.code.gson, name:gson, version:2.8.6
    // 'group:name:version:classifier' where classifier is optional

    // Example - Deobfuscating dependencies:
    // rfg.deobf('curse.maven:had-enough-items-557549:4543375')
    // By wrapping a dependency descriptor in rfg.deobf() method call, the dependency is queued for deobfuscation
    // When deobfuscating, RFG respects the mapping_channel + mapping_version stated in gradle.properties

    // Example - CurseMaven dependencies:
    // 'curse.maven:had-enough-items-557549:4543375' << had-enough-items = project slug, 557549 = project id, 4543375 = file id
    // Full documentation: https://cursemaven.com/

    // Example - Modrinth dependencies:
    // 'maven.modrinth:jei:4.16.1.1000' << jei = project name, 4.16.1.1000 = file version
    // Full documentation: https://docs.modrinth.com/docs/tutorials/maven/

    // Common dependency types (configuration):
    // implementation = dependency available at both compile time and runtime
    // runtimeOnly = runtime dependency
    // compileOnly = compile time dependency
    // annotationProcessor = annotation processing dependencies

    // Transitive dependencies:
    // (Dependencies that your dependency depends on)
    // If you wish to exclude transitive dependencies in the described dependencies
    // Use a closure as such:
    // implementation ('com.google.code.gson:gson:2.8.6') {
    //     transitive = false
    // }

    implementation rfg.deobf("curse.maven:had-enough-items-557549:5471944")
    implementation("com.cleanroommc:groovyscript:1.2.0-hotfix1") { transitive = false } // so it doesn't installs JEI

    implementation rfg.deobf("curse.maven:patchouli-rofl-edition-905159:5201041")

    // Runtime dependencies
    runtimeOnly "curse.maven:the-one-probe-community-edition-885670:5824252"
    runtimeOnly "curse.maven:topalldependents-1120632:5808089" // updated version of the one probe addons
    runtimeOnly rfg.deobf("curse.maven:vintagefix-871198:5010926") // otherwise it will crash
    runtimeOnly rfg.deobf("curse.maven:modern-splash-629058:5282442") // otherwise it will crash

    // runtimeOnly "curse.maven:smooth-font-285742:3944565"

    runtimeOnly "com.cleanroommc:configanytime:3.0"
    runtimeOnly "curse.maven:red-core-873867:5618189"
    runtimeOnly "curse.maven:alfheim-lighting-engine-910715:5637644"
    runtimeOnly "curse.maven:valkyrie-874067:5235356"

    // Mod Compatibility
    // Dependencies
    if (project.debug_moarboats.toBoolean()) {
        runtimeOnly "curse.maven:forgelin-continuous-456403:5832299"
    }

    if (project.debug_pressure.toBoolean() || project.debug_gendustry.toBoolean()) {
        runtimeOnly "curse.maven:bdlib-70496:4675629"
    }

    if (project.debug_gendustry.toBoolean()) {
        runtimeOnly "curse.maven:forestry-59751:2684780"
    }

    if (project.debug_thermalexpansion.toBoolean() || project.debug_thermallogistics.toBoolean()) {
        runtimeOnly "curse.maven:codechicken-lib-1-8-242818:2779848"
    }

    if (project.debug_thermalexpansion.toBoolean() || project.debug_thermallogistics.toBoolean()) {
        runtimeOnly "curse.maven:redstone-flux-270789:2920436"
        runtimeOnly "curse.maven:cofh-core-69162:2920433"
        runtimeOnly "curse.maven:cofh-world-271384:2920434"
        runtimeOnly "curse.maven:thermal-foundation-222880:2926428"
    }

    if (project.debug_thermallogistics.toBoolean()) {
        runtimeOnly "curse.maven:thermal-dynamics-227443:2920505"
    }

    // Content Mods
    // Gendustry
    if (project.debug_gendustry.toBoolean()) {
        runtimeOnly "curse.maven:gendustry-70492:2516215"
    }

    // Moar Boats
    if (project.debug_moarboats.toBoolean()) {
        runtimeOnly "curse.maven:moar-boats-285865:2733014"
    }

    // Pressure Pipes
    if (project.debug_pressure.toBoolean()) {
        runtimeOnly "curse.maven:pressure-pipes-221214:2476931"
    }

    // Thermal Expansion
    if (project.debug_thermalexpansion.toBoolean()) {
        runtimeOnly "curse.maven:thermal-expansion-69163:2926431"
    }

    // Thermal Logistics
    if (project.debug_thermallogistics.toBoolean()) {
        runtimeOnly "curse.maven:thermal-logistics-313298:3379693"
    }
}